# vim: ft=zsh:
#
# Based on pure: https://github.com/sindresorhus/pure
#
###
_git_info() {
  vcs_info

  printf "$vcs_info_msg_0_" # branch name/action

  command git rev-parse --is-inside-work-tree &>/dev/null || return
  command git diff --quiet --ignore-submodules HEAD &>/dev/null

  (( $? == 1 )) && printf '*'
}

_ruby_info() {
  which ruby &>/dev/null || return

  printf ' ruby-%s' "$(ruby --version | cut -d' ' -f2)"
}

prompt_pbr_precmd() { print -Pn "\e]0;%~\a"; }

prompt_pbr_preexec() { printf "\e]0;$PWD:t: $2\a"; }

prompt_pbr_setup() {
  prompt_opts=( cr subst percent )

  autoload -Uz vcs_info
  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_pbr_precmd
  add-zsh-hook preexec prompt_pbr_preexec

  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:git*' formats ' %b'
  zstyle ':vcs_info:git*' actionformats ' %b|%a'

  [[ -n "$SSH_CONNECTION" ]] && prompt_pbr_username=' %n@%m'

  PROMPT='
%F{blue}%~%F{8}$(_ruby_info)$(_git_info)$prompt_pbr_username%f
%(?.%F{magenta}.%F{red})‚ùØ%f '
}

prompt_pbr_setup "$@"
